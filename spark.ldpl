#
# Spark
# Ported from Bash to LDPL by Simon Dann
#
# Generates sparklines for a set of data.
#
# spark takes a space-separated list of data and then prints
# a sparkline out of it.
#
# Example:
#
#   $ ldpl spark 1 5 22 13 53
#   # => ▁▁▃▂▇
#
#   spark -h|--help
#   # => Prints the spark help text.
#
#   spark -v|--version
#   # => Prints the spark version  text.
#
# Source project: https://github.com/holman/spark
#

DATA:

const.ticks IS TEXT VECTOR

min IS NUMBER
max IS NUMBER
output IS TEXT

i IS NUMBER
n IS NUMBER

normalise.x IS NUMBER
normalise.a IS NUMBER
normalise.b IS NUMBER

PROCEDURE:

#
# Initial state for min/max
#
STORE 0 IN min
STORE 0 IN max
STORE "" IN output

#
# Constants
#
STORE "▁" IN const.ticks:0
STORE "▂" IN const.ticks:1
STORE "▃" IN const.ticks:2
STORE "▄" IN const.ticks:3
STORE "▅" IN const.ticks:4
STORE "▆" IN const.ticks:5
STORE "▇" IN const.ticks:6
STORE "█" IN const.ticks:7

#
# If argument is -h or --help, display usage and exit
#
SUB-PROCEDURE help
    DISPLAY "USAGE:" CRLF
    DISPLAY "  ldpl spark [-v|--version|-h|--help] VALUE,..." CRLF CRLF

    DISPLAY "EXAMPLES:" CRLF
    DISPLAY "  $ ldpl spark 1 5 22 13 53" CRLF
    DISPLAY "▁▁▃▂█" CRLF
    DISPLAY "  $ ldpl spark 0 30 55 80 33 150" CRLF
    DISPLAY "▁▂▃▄▂█" CRLF
    DISPLAY "  $ ldpl spark 9 13 5 17 1" CRLF
    DISPLAY "▄▆▂█▁" CRLF
    EXIT
END SUB-PROCEDURE

#
# If argument is -v or --version, display version and exit
#
SUB-PROCEDURE version
    DISPLAY "Version 1.0.0 - 5th March 2019" CRLF
END SUB-PROCEDURE

#
# Find Min/Max Values
#
SUB-PROCEDURE find-min-max
    STORE 0 IN i
    WHILE i IS LESS THAN argc DO
        IF i IS EQUAL TO 0 THEN
            STORE argv:i IN min
            STORE argv:i IN max
        END IF

        STORE argv:i IN n

        IF n IS LESS THAN min THEN
            STORE argv:i IN min
        END IF

        IF n IS GREATER THAN max THEN
            STORE argv:i IN max
        END IF

        ADD i AND 1 IN i
    REPEAT
END SUB-PROCEDURE

#
# Normalise Values between 0 and 7
# (maxAllowed - minAllowed) * (unscaledNum - min) / (max - min) + minAllowed
#
SUB-PROCEDURE normalise
    SUBTRACT min FROM max IN normalise.x
    IF normalise.x IS LESS THAN 0 THEN
        STORE 0 IN normalise.x
    END IF

    STORE 0 IN i
        WHILE i IS LESS THAN argc DO
            STORE argv:i IN n
            SUBTRACT min FROM n IN normalise.a
            DIVIDE normalise.a BY normalise.x IN normalise.b
            MULTIPLY normalise.b BY 7 IN normalise.a
            FLOOR normalise.a
            JOIN const.ticks:normalise.a AND output IN output
        ADD i AND 1 IN i
    REPEAT
END SUB-PROCEDURE

IF argc IS LESS THAN OR EQUAL TO 1 THEN
    IF argc IS EQUAL TO 1 THEN
        IF argv:0 IS EQUAL TO "-h" THEN
            CALL SUB-PROCEDURE help
        END IF

        IF argv:0 IS EQUAL TO "--help" THEN
            CALL SUB-PROCEDURE help
        END IF

        IF argv:0 IS EQUAL TO "-v" THEN
            CALL SUB-PROCEDURE version
        END IF

        IF argv:0 IS EQUAL TO "--version" THEN
            CALL SUB-PROCEDURE version
            EXIT
        END IF
    END IF
END IF

CALL SUB-PROCEDURE find-min-max
CALL SUB-PROCEDURE normalise
DISPLAY output CRLF
